
import org.babeloff.ProjectVersion
import org.babeloff.ReleaseVersionTask
import org.babeloff.SnapshotVersionTask

// apply from: 'gradle/foo.gradle'

plugins {
  id 'java'
  id 'maven'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '6.1.0'
  id 'idea'
  id 'eclipse'
}

group = 'net.catdata'

project.ext.versionFile = file('gradle/version.properties')

mainClassName = "catdata.ide.IDE"

clean.doFirst {
  delete fileTree("${project.projectDir}") {
    include '**/*.log'
  }
}

task loadVersion {
    project.version = ProjectVersion.read(logger, versionFile)
}

task makeReleaseVersion (type: ReleaseVersionTask) {
    release = version.release
    propFile = versionFile
}

task makeSnapshotVersion (type: SnapshotVersionTask) {
    propFile = versionFile
}

task showVersion {
    group = 'versioning'
    description = 'Prints project version.'

    doLast {
        logger.quiet "Version : ${project.version}"
    }
}

task showDependencies {
    doLast {
        configurations.getByName('antlr').each{
            dependency -> println dependency
        }
    }
}

task copyDependencies (type: Sync) {
  from configurations.default
  into 'lib'
  include '**/*.jar'
}

description = "Conexus Categorical Data IDE"

sourceCompatibility = 1.15
targetCompatibility = 1.15

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task createDistribution(type: Zip, dependsOn: makeReleaseVersion) {
    from jar

    from(sourceSets*.allSource) { into 'src' }
    from(rootDir) { include versionFile.name }
}

task backupReleaseDistribution(type: Copy) {
    from createDistribution.outputs.files
    into "$buildDir/backup"
}

task release(dependsOn: backupReleaseDistribution) {
    doLast { logger.quiet 'releasing the project...' }
}

configurations {
    all {   }
}

wrapper {
    gradleVersion = '6.3'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

sourceSets {
    main {
        resources {
            srcDirs = ["${project.projectDir}/resources"]
        }
    }
    test {
        resources {
            srcDirs = ["${project.projectDir}/resources"]
        }
    }
}

idea {
    module {
        generatedSourceDirs += file("${project.buildDir}/generated-src/java")
        downloadJavadoc = true
        downloadSources = false
    }
}

repositories {
    mavenCentral()
    maven { url "https://nexus.isis.vanderbilt.edu/repository/maven-releases" }
    maven { url "https://nexus.isis.vanderbilt.edu/repository/maven-snapshots" }
    ivy { url "https://www.sosy-lab.org/ivy" }
}

dependencies {
    implementation group: 'org.jparsec', name: 'jparsec', version:'3.0'
    implementation group: 'net.sf.jung', name: 'jung-algorithms', version:'2.1.1'
    implementation group: 'net.sf.jung', name: 'jung-api', version:'2.1.1'
    implementation group: 'net.sf.jung', name: 'jung-graph-impl', version:'2.1.1'
    implementation group: 'net.sf.jung', name: 'jung-visualization', version:'2.1.1'
    implementation group: 'com.fifesoft', name: 'autocomplete', version:'2.6.1'
    implementation group: 'com.fifesoft', name: 'rsyntaxtextarea', version:'3.1.1'
    implementation group: 'com.fifesoft', name: 'rstaui', version:'2.6.1'
    implementation group: 'org.apache.commons', name: 'commons-csv', version:'1.5'
    implementation group: 'org.apache.commons', name: 'commons-exec', version:'1.3'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.0'
    implementation group: 'com.h2database', name: 'h2', version:'1.4.196'
    implementation group: 'com.opencsv', name: 'opencsv', version:'4.1'
    implementation group: 'jgraph', name: 'jgraph', version:'5.12.3.2'
    implementation group: 'org.glassfish', name: 'javax.json', version:'1.1.2'
    implementation group: 'org.json', name: 'json', version: '20140107'
    implementation group: 'es.nitaur.markdown', name: 'txtmark', version:'0.16'
    implementation group: 'org.graalvm.js', name: 'js', version: '20.2.0'
    implementation group: 'org.graalvm.js', name: 'js-scriptengine', version: '20.2.0'
    implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.16.1'
    implementation group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    implementation group: 'info.picocli', name: 'picocli', version:'4.6.1'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.5.13'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient-cache', version:'4.4.13'
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version:'4.4.14'
    implementation group: 'org.apache.jena', name: 'jena-arq', version:'3.17.0'
    implementation group: 'org.apache.jena', name: 'jena-core', version:'3.17.0'
    implementation group: 'org.apache.jena', name: 'jena-base', version:'3.17.0'
    implementation group: 'org.apache.jena', name: 'jena-shaded-guava', version:'3.17.0'
    implementation group: 'org.apache.jena', name: 'jena-iri', version:'3.17.0'
    implementation group: 'org.apache.tinkerpop', name: 'gremlin-core', version: '3.4.10'
    implementation group: 'org.apache.tinkerpop', name: 'gremlin-groovy', version: '3.4.10'
    implementation group: 'org.apache.tinkerpop', name: 'gremlin-server', version: '3.4.10'
    implementation group: 'com.lambdazen.bitsy', name: 'bitsy', version: '3.4.2'
    implementation group: 'com.formdev', name: 'flatlaf', version:'1.0'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version:'5.3.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version:'5.3.1'
    testRuntimeOnly group:'org.junit.jupiter', name:'junit-jupiter-engine', version:'5.3.1'
}

test {
    useJUnitPlatform()
}

shadowJar {
  project.configurations.implementation.canBeResolved = true
  configurations = [project.configurations.implementation]
  mergeServiceFiles()
}

assemble.dependsOn shadowJar

task generateManual (type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = 'catdata.aql.exp.AqlHelp'
}
